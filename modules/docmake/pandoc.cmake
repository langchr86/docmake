cmake_minimum_required(VERSION 3.10)

include(docmake/helpers)

prepare_tool(pandoc PANDOC_EXECUTABLE)


#! pandoc_resource_files : Add additional resource files to a pandoc_document target.
#
# This adds a simple copy step for each specified file.
# The resource files are copied into the output folder with the exact same file structure. See helper_prepare_file_paths.
# All those steps are added to the dependencies of the pandoc_document target.
#
# This is needed to have the exact same relative file path in the input files for pandoc later when pandoc is executed.
# Pandoc is executed in the folder where the output file will be placed.
# Like this the document can directly use this relatie path to e.g. reference image files.
#
# This calls add_custom_command for each resource file. This can process multiple input files at ones.
#
# \arg:TARGET The cmake target generated by pandoc_document.
# \arg:ROOT_PATH Root path from which the folder structure should be recreated in the output folder.
# \argn: List of the resource files to add.
#
function(pandoc_resource_files TARGET ROOT_PATH)
    set(FILES ${ARGN})

    get_target_property(resource_list ${TARGET} RESOURCE_FILES)

    foreach(file ${FILES})
        helper_prepare_file_paths(
                INPUT_FILE ${file}
                INPUT_ROOT_PATH ${ROOT_PATH}
                INPUT_DEST_PATH ${CMAKE_CURRENT_BINARY_DIR}
                OUTPUT_ABSOLUTE_SOURCE_FILE absolute_source
                OUTPUT_RELATIVE_FILE relative_file
                OUTPUT_ABSOLUTE_DEST_FILE absolute_dest_file
                OUTPUT_ABSOLUTE_DEST_FOLDER absolute_dest_folder
        )

        add_custom_command(
                OUTPUT ${absolute_dest_file}
                COMMAND ${CMAKE_COMMAND} -E copy ${absolute_source} ${absolute_dest_folder}
                DEPENDS ${absolute_source}
                COMMENT "Copying pandoc resource file to working dir: ${relative_file}"
                VERBATIM
        )

        list(APPEND resource_list ${absolute_dest_file})
    endforeach()

    set_target_properties(${TARGET} PROPERTIES RESOURCE_FILES "${resource_list}")
endfunction()


#! pandoc_document : Compilation step of an arbitrary input file into an arbitrary output file.
#
# This adds a simply copy step for each specified file.
# The resource files are copied into the output folder with the exact same file structure. See helper_prepare_file_paths.
# All those steps are added to the pandoc_document target.
#
# This calls add_custom_command for each resource file. This can process multiple input files at ones.
#
# \param:TARGET The to generate cmake target. This needs to be named after the output file incl. extension. This will trigger the correct export format in pandoc.
# \param:TEMPLATE The path to the template file which is used in the --template parameter.
# \group:SOURCES List of all files that are input of the conversion step.
# \group:HEADERS List of file paths to header files that are used in the --include-in-header parameter.
# \group:PARAMS List of arbitrary other commands to give to the pandoc execution.
# \group:DEPENDS Additional non-input files on which the cmake target depends. E.g. timestamp files of a lint step.
#
function(pandoc_document)
    set(oneValueArgs TARGET TEMPLATE)
    set(multiValueArgs SOURCES HEADERS PARAMS DEPENDS)
    cmake_parse_arguments(PANDOC_DOCUMENT "" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

    set(output_name ${PANDOC_DOCUMENT_TARGET})
    set(output_path ${CMAKE_CURRENT_BINARY_DIR}/${output_name})

    if(PANDOC_DOCUMENT_TEMPLATE)
        get_filename_component(template_path ${PANDOC_DOCUMENT_TEMPLATE} ABSOLUTE)
        set(template_arg --template=${template_path})
    endif()

    set(source_list)
    foreach(source ${PANDOC_DOCUMENT_SOURCES})
        get_filename_component(source_path ${source} ABSOLUTE)
        list(APPEND source_list ${source_path})
    endforeach()

    set(header_list)
    foreach(header ${PANDOC_DOCUMENT_HEADERS})
        get_filename_component(header_path ${header} ABSOLUTE)
        list(APPEND header_list ${header_path})
        list(APPEND header_arg --include-in-header=${header_path})
    endforeach()

    add_custom_target(
            ${PANDOC_DOCUMENT_TARGET}
            ALL
            DEPENDS ${output_path}
    )

    set_target_properties(${PANDOC_DOCUMENT_TARGET} PROPERTIES RESOURCE_FILES "")

    add_custom_command(
            OUTPUT ${output_path}
            COMMAND ${CMAKE_COMMAND} -E chdir ${CMAKE_CURRENT_BINARY_DIR}
            ${PANDOC_EXECUTABLE} ${source_list}
            -o ${output_path}
            --fail-if-warnings
            ${header_arg}
            ${template_arg}
            ${PANDOC_DOCUMENT_PARAMS}
            --log=${output_path}.log
            DEPENDS ${source_list} ${header_list} ${template_path}
            DEPENDS $<TARGET_PROPERTY:${PANDOC_DOCUMENT_TARGET},RESOURCE_FILES>
            DEPENDS ${PANDOC_DOCUMENT_DEPENDS}
            COMMENT "Building pandoc document: ${output_name}"
            COMMAND_EXPAND_LISTS
            VERBATIM
    )
endfunction()
